; various subroutines such as memcpys, specific memcpys,
; OAM DMA, joy polling, basic SGB communication

; theres also optimized div and mul, maybe ill go
; crazy and write even more maths stuff?

; i dont know why youd use this over pinos stuff

; the comments may be a bit lacking, but thats fine

; if for any crazy reason you do use any of these
; then please credit me, and like let me know,
; it makes the time i spent on this not feel like a waste

INCLUDE "hardware.inc" ; hardware definitions, really important
INCLUDE "macros.inc" ; helps with readability, at least a bit

section "memcpy", rom0
MemCpy:: ; hl - src, de - dest, bc - length
	; adjust for decloop
	dec bc
	inc b
	inc c
.loop ; copy a byte
	ld a, [hl+]
	ld [de], a
	inc de
	; decrement length
	dec c
	jr nz, .loop
	dec b
	jr nz, .loop
	ret

section "shortcpy", rom0
ShortCpy:: ; hl - src, de - dest, b - length
.loop ; copy a byte
	ld a, [hl+]
	ld [de], a
	inc de
	; decrement length
	dec b
	jr nz, .loop
	ret

section "memset", rom0
MemSet:: ; a - data, hl - dest, bc - length, clobbers e
	; adjust bc
	dec bc
	inc b
	inc c
.loop ; set a byte
	ld [hl+], a
	; decrement length
	dec c
	jr nz, .loop
	dec b
	jr nz, .loop
	ret

section "shortset", rom0
ShortSet:: ; a - data, hl - dest, b - length
.loop ; copy a byte, short as fu-
	ld [hl+], a
	; decrement length
	dec b
	jr nz, .loop
	ret

section "palcpy", rom0
PalCpy:: ; hl - src, a - F_AUTOINC | dest, c - rOCPS/rBCPS, b - length
	; set addr
	ldh [c], a
	inc c
.loop ; copy a byte
	ld a, [hl+]
	ldh [c], a ; pal
	; dec length
	dec b
	jr nz, .loop
	ret

	section "safepalcpy", rom0
SafePalCpy:: ; hl - src, a - F_AUTOINC | dest, c - rOCPS/rBCPS, b - length / 4
	; set addr
	ldh [c], a
	inc c
.loop ; wait for access
	ldh a, [rSTAT]
	and STATF_BUSY
	jr nz, .loop
	; copy 4 bytes
	rept 4
		ld a, [hl+]
		ldh [c], a ; pal
		endr
	; dec length
	dec b
	jr nz, .loop
	ret

section "safecpy", rom0
SafeCpy:: ; hl - src, de - dest, bc - length, align 1!!
	; adjust for unroll (2), then adjust bc
	srl b
		rr c
	dec bc
	inc b
	inc c
.loop ; wait for safe VRAM access
	ldh a, [rSTAT]
	and STATF_BUSY
	jr nz, .loop
	; once its safe copy 2 bytes
	ld a, [hl+]
	ld [de], a
	inc e
	ld a, [hl+]
	ld [de], a
	inc de
	; check if all bytes got copied
	dec c
	jr nz, .loop ; repeat
	dec b
	jr nz, .loop
	ret

section "safe1bppcpy", rom0
Safe1bppCpy:: ; hl - src, de - dest, bc - length, align 1!!
	; adjust for unroll (2), then adjust bc
	srl b
		rr c
	dec bc
	inc b
	inc c
.loop ; wait for safe VRAM access
	ldh a, [rSTAT]
	and STATF_BUSY
	jr nz, .loop
	; once its safe copy two bytes
	for V, 2
		ld a, [hl+]
		ld [de], a
		inc e
		ld [de], a
		if V == 1
			inc de
		else
			inc e
		endc
	endr
	; check if all bytes got copied
	dec c
	jr nz, .loop ; repeat
	dec b
	jr nz, .loop
	ret

section "safeset", rom0
SafeSet:: ; a - data, hl - dest, bc - length, clobbers e, align 2!!
	; adjust for unroll (4), then adjust bc
	rept 2
		srl b
		rr c
		endr
	dec bc
		inc b
		inc c
	ld e, a ; a gets clobbered in the "safe" part
.loop ; wait for safe VRAM access
	ldh a, [rSTAT]
	and STATF_BUSY
	jr nz, .loop
	; once its safe set 4 bytes
	ld a, e ; restore accumulator
	ld [hl+], a
	ld [hl+], a
	ld [hl+], a
	ld [hl+], a
	; check if all bytes got set
	dec c
	jr nz, .loop
	dec b
	jr nz, .loop ; repeat
	ret

; this is a popslide, as the name suggests it
; pops values off the stack, as its faster for
; copying lots of data (32 bytes or more)
; unfortunately it needs disabled interrupts
section "popslide", rom0
Popslide:: ; a - length, hl - src, de - dest, clobbers bc, align 4!!
	ld [wStack], sp ; save sp
	; PLEASE make sure IME=0
	ld sp, hl
	ld h, d
	ld l, e
	.loop
	for V, 16/2
		pop bc
		ld [hl], c
		inc l
		ld [hl], b
		if V == 7
			inc hl
		else
			inc l
		endc
	endr
	dec a
	jr nz, .loop
	ld sp, wStack
	pop hl
	ld sp, hl
	ret

section "DMA", rom0
DMAcopy:: ; copy this to HRAM then call hDMA
load "hDMA", hram
hDMA:: ; a = high(ShadowOAM)
	ldh [rDMA], a
	ld a, 40
	.wait
	dec a
	jr nz, .wait
	ret
	.end::
endl

; allocate shadow OAM
section "shadowOAM", wram0, align[8]
	wSOAM:: ds 160
	.index:: ds 1
	.end::

if def(PLAYERS) == 0
	def PLAYERS equs "1" ; default to 1 player
endc

section "joy", rom0 ; wow this is such a mess
Joy:: ; reads joypad, b - no. of joypads (if more than 1), clobbers (b)c, hl, de
	ld hl, hPad1
	ld c, low(rP1)
.loop
	ld e, [hl] ; preserve for later
	ldh a, [c] ; get Dpad
	ld d, a
	ld a, P1F_GET_BTN
	ldh [c], a ; select buttons
	ld a, $0f
	and d
	swap a ; dpad goes in left nibble
	ld d, a
	ldh a, [c] ; get buttons
	and a, $0f
	or a, d ; merge states
	cpl ; invert
	ld [hl+], a ; buttons held
	xor a, e
	ld [hl+], a ; buttons changed
	and e
	ld [hl+], a ; buttons newly pressed
	ld a, P1F_GET_DPAD
	ldh [c], a ; select Dpad
	if PLAYERS > 1
		dec b
		jr nz, .loop
	endc
	ret

section "joypads", hram
for V, 1, PLAYERS+1
	hPad{x:V}:: ds 1
	.diff:: ds 1
	.press:: ds 1
endr

; someone please if youre reading this remind me to
; rewrite this entire thing at some point or something
section "packet", rom0 ; this looks like a mess..
Packet:: ; sends an SGB packet, hl - src
	lb bc, 16, low(rP1)
	xor a
		ldh [c], a ; 5 cycles
	ld a, $ff ; end pulse
		nop
		ldh [c], a ; 15 cycles
	.byte
		ld d, [hl]
		inc hl
		ld e, 8
		.bit
			xor a ; load A with SGB bit
				rr d ; fetch next bit
				ccf ; set accumulator in the dumbest way i could come up with
				adc a, a
				inc a
				swap a
				rept 2
					nop
					endr
				ldh [c], a ; 5 cycles
			ld a, $ff ; end pulse
				nop
				ldh [c], a ; 15 cycles
			dec e
			jr nz, .bit
		dec b
		jr nz, .byte
	ld a, $20 ; stop bit
		rept 6
			nop
			endr
		ldh [c], a
	ld a, $ff ; hi?
		nop
		;nop
		ldh [c], a
	ld a, P1F_GET_DPAD ; reselect buttons
		rept 11
			nop
			endr
		ldh [c], a
	ret

section "multiply", rom0
; unsigned multiplication,
; using (if im not mistaken) binary long multiplication
; this takes * 12MSSBb + BS + 22 M-cycles (up to 114 M-cycles)
; where MSSBb is the most significant set bit of b
; and BS is number of set bits in b register
; (for best(better) result put the lower value in b)
Multiply:: ; hl = b * c, clobbers a (a = 0)
	ld hl, 0
	ld a, b
	ld b, 0
	or a ; clears carry
.loop
	rra ; check bit 0, shift a, carry must be reset
	jr nc, :+
	add hl, bc ; add if carry set
:	sla c ; shift bc left
	rl b
	and a ; end as soon as out of bits, also clears carry
	jr nz, .loop
	ret

section "divide", rom0
; unsigned division,
; using binary long division
; big thanks to calc84maniac for pointing out a "nonbinding" optimization
; and evie/eievui for helping test and debug!
; division by zero returns a=l, hl=$ffff
; up to 203 M-cycles if a's 7th bit is reset, 236 M-cycles otherwise
; new optimization idea: only run the carry check on a=128..255
Divide:: ; hl = hl / a, a = hl % a, clobbers de
	ld d, a
	ld e, 16 ; you could check if H is 0 and run for only 8 bits maybe?
	xor a
	bit 7, d
	jr nz, .loop8 ; 8 bit loop has a failsafe, at a small cycle penalty
	for V, 7, 9
.loop{d:V}
	add hl, hl ; shift hla left
	rla
	if V == 8 ; only for 8 bit loop
		jr c, .more{d:V} ; always sub if a overflows
	endc
	cp a, d ; check if can sub
	jr c, .less{d:V}
	.more{d:V} ; sub and shift a bit in hla
	sub d
	inc l
	.less{d:V}
	dec e ; repeat
	jr nz, .loop{d:V}
	ret
	endr

section "crc", rom0
def CRC32_POLY equ $EDB88320 ; reflected $04c11db7
; this would be crc32ccitt if it was actually fucking
; properly documented i am so fucking mad who the fuck
; even wrote the god damn documentation
; please write your own converter or something if you want to use this
; cause i am not going to make it 1:1 compatibile with crc32ccitt untill someone
; writes actual documentation that actually explains what the fuck is going on there
; de - data to checksum, bc - data length
; hl = pointer to a 4 byte big endian checksum
; clobbers all registers
Crc32::
	inc bc ; it will skip processing the
		; last byte otherwise
	; init buffer
	ld hl, wCrc.crc
	ld a, $ff
	rept 4
		ld [hl+], a
	endr
.load ; load a byte
	ld a, [de]
	ld [wCrc.input], a
	inc de
	dec bc
	; check for end of data
	ld a, b
	or a, c
	jr z, .end
	push bc ; save byte counter
	ld b, 8 ; use b as bit counter instead
.shift ; shift the buffer
	ld l, low(wCrc.input)
	for V, 5
		rr [hl]
		if V < 4
			inc l
				assert high(wCrc) == high(wCrc.end)
		endc
	endr
	jr c, .divide ; check for overflow
	dec b ; decrement bit count
	jr nz, .shift ; if bits > 0 keep going
	pop bc ; restore byte counter
	jr .load ; if out of bits load next byte

.divide ; divide buffer
	ld l, low(wCrc.crc)
	for V, 4 ; xor each byte with the polynomial
		ld a, [hl]
		xor low(CRC32_POLY>>((3-V)*8))
		ld [hl+], a
	endr
	dec b ; decrement bit count
	jr nz, .shift ; if bits > 0 keep going
	pop bc ; restore byte counter
	jr .load ; if out of bits load next byte

.end ; make hl point at the checksum
		ld l, low(wCrc.end)
		ld b, 4
			assert wCrc.end - 4 == wCrc.crc
			; make sure that hl will be wCrc.crc when returning
		.outXorLoop ; bitflip the checksum
		dec l
		ld a, [hl]
		cpl
		ld [hl], a
		dec b
		jr nz, .outXorLoop
	ret

section "crcram", wram0, align[3]
wCrc::
	.input ds 1 ; 8 bit buffer for bit by bit input processing
	.crc   ds 4 ; 32 bit buffer for processing the CRC
	.end

; allocate a stack
; if no stack size passed, default to 32
if ISCONST(STACK_SIZE) != 1 || def(STACK_SIZE) != 1
	def STACK_SIZE equ 32
endc

section "stack", wram0[$d000-(STACK_SIZE*2)] ; put the stack at the end
wStack:: ds (STACK_SIZE*2) ; stack operates on words
.origin:: ; ld sp, stack.origin